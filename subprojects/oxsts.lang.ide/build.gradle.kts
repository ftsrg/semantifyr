/*
 * SPDX-FileCopyrightText: 2023-2025 The Semantifyr Authors
 *
 * SPDX-License-Identifier: EPL-2.0
 */

plugins {
    id("hu.bme.mit.semantifyr.gradle.conventions.application")
}

val ideGeneratedClasspath by configurations.creating {
    isCanBeConsumed = false
    isCanBeResolved = true
}

dependencies {
    api(project(":oxsts.lang"))

    implementation(project(":semantics"))
    implementation(project(":xsts-verifier"))

    api(libs.xtext.ide)
    runtimeOnly(libs.slf4j.log4j)

    ideGeneratedClasspath(project(":oxsts.lang", configuration = "ideGeneratedOutput"))
}

sourceSets {
    main {
        java.srcDir("src/main/xtext-gen")
    }
}

tasks {
    val cloneIdeGenerated by registering(Sync::class) {
        inputs.files(ideGeneratedClasspath)

        from(ideGeneratedClasspath.asFileTree)
        into("src/main/xtext-gen")
    }

    compileJava {
        inputs.files(cloneIdeGenerated.get().outputs)
    }

    clean {
        delete("src/main/xtext-gen")
    }
}

application {
    mainClass = "hu.bme.mit.semantifyr.oxsts.lang.ide.OxstsIdeSetup"
}
