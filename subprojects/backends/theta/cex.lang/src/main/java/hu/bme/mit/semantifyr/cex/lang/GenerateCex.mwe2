/*
 * SPDX-FileCopyrightText: 2023-2024 The Semantifyr Authors
 *
 * SPDX-License-Identifier: EPL-2.0
 */

module hu.bme.mit.semantifyr.cex.lang.GenerateCex

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "cex.lang"
				rootPath = rootPath
				runtime = {
					src = null
				}
				runtimeTest = {
					enabled = true
					// Only generate the xtext-gen files and leave the rest of the project alone
					root = null
					srcGen = '${rootPath}/cex.lang/src/testFixtures/xtext-gen'
				}
				genericIde = {
					enabled = true
					root = null
                    srcGen = "${rootPath}/cex.lang/build/generated/sources/xtext/ide"
                }
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "hu.bme.mit.semantifyr.cex.lang.Cex"
			fileExtensions = "cex"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

            serializer = {
                generateStub = true
            }
            formatter = {
                generateStub = true
            }
			validator = {
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = false
			}
			junitSupport = {
				generateStub = false
				skipXbaseTestingPackage = true
				junitVersion = '5'
			}
		}
	}
}
