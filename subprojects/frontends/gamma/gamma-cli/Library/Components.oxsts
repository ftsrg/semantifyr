/*
 * SPDX-FileCopyrightText: 2025 The Semantifyr Authors
 *
 * SPDX-License-Identifier: EPL-2.0
 */

package semantifyr::gamma::components

import semantifyr::gamma::events

class Component {
    contains events: Event[0..*]
    contains inputEvents: Event[0..*] subsets events
    contains outputEvents: Event[0..*] subsets events

    contains timeouts: Timeout[0..*]

    tran resetInputEvents() {
        inline for (event in inputEvents) {
            inline event.reset()
        }
    }

    tran havocInputEvents() {
        inline for (event in inputEvents) {
            inline event.havoc()
        }
    }

    tran resetOutputEvents() {
        inline for (event in outputEvents) {
            inline event.reset()
        }
    }

    tran passTime() {
        inline for (timeout in timeouts) {
            inline timeout.passTime()
        }
    }
}

class Channel {
    refers inputEvent: Event[1]
    refers outputEvent: Event[1]

    redefine tran {
        if (inputEvent.isActive) {
            inline outputEvent.set()
        }
    }
}

class CompositeComponent : Component {
    contains components: Component[0..*]
    contains channels: Channel[0..*]

    redefine tran passTime() {
        inline for (timeout in timeouts) {
            inline timeout.passTime()
        }

        inline for (component in components) {
            inline component.passTime()
        }
    }
}

class SyncComponent : CompositeComponent {
    redefine init {
        inline for (component in components) {
            inline component.init()
        }
    }

    redefine tran {
        inline resetOutputEvents()

        inline for (component in components) {
            inline component.main()
        }

        inline for (channel in channels) {
            inline channel.main()
        }

        inline resetInputEvents()
    }
}
