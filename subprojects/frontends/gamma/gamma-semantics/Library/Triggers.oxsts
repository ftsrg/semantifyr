/*
 * SPDX-FileCopyrightText: 2025 The Semantifyr Authors
 *
 * SPDX-License-Identifier: EPL-2.0
 */

package semantifyr::gamma::triggers

import semantifyr::gamma::events
import semantifyr::gamma::ports

class Trigger {
    prop isTriggered() {
        return false
    }

    tran assumeIsTriggered() {
    }
}

class EventTrigger : Trigger {
    refers port: Port[1]
    refers event: Event[1]

    redefine prop isTriggered() {
        return port.hasInputEvent(event)
    }

    redefine tran assumeIsTriggered() {
        inline port.assumeHasInputEvent(event)
    }
}

class TimeoutTrigger : Trigger {
    refers timeout: Timeout[1]

    redefine prop isTriggered() {
        return timeout.isUp()
    }

    redefine tran assumeIsTriggered() {
        inline timeout.assumeIsUp()
    }
}

class Timeout {
    refers deltaTime: int = 1

    var remainingTime: int := 0

    tran passTime() {
        if (remainingTime > 0) {
            remainingTime := remainingTime - deltaTime
        }
    }

    prop isUp() {
        return remainingTime <= 0
    }

    tran assumeIsUp() {
        assume (remainingTime <= 0)
    }
}
