package semantifyr::sysml::parts

import semantifyr::sysml::attributes
import semantifyr::sysml::ports
import semantifyr::sysml::states

class Part {
    contains parts: Part[0..*]
    contains attributes: Attribute[0..*]
    contains ports: Port[0..*]
    contains exhibitState: AbstractState[0..1]
    contains flows: Flow[0..*]

    tran resetInputEvents() {
        inline for (port in ports) {
            inline port.resetIncomingItem()
        }
    }

    tran havocInputEvents() {
        inline for (port in ports) {
            inline port.havocIncomingItem()
        }
    }

    tran resetOutputEvents() {
        inline for (port in ports) {
            inline port.resetOutgoingItem()
        }
    }

    tran passTime() {
        inline for (part in parts) {
            inline part.passTime()
        }

        inline exhibitState?.passTime()
    }

    tran executeExhibitState() {
        choice {
            inline exhibitState?.fireTransitions()
        } or {
            // do nothing, if no transition is enabled
            inline exhibitState?.assumeNoTransitionEnabled()
        }
    }

    redefine init {
        inline exhibitState?.activateRecursive()

        inline for (part in parts) {
            inline part.init()
        }
    }

    redefine tran {
        inline resetOutputEvents()

        inline for (part in parts) {
            inline part.main()
        }

        inline executeExhibitState()

        inline for (flow in flows) {
            inline flow.main()
        }

        inline resetInputEvents()
    }
}

class Flow {
    refers inputPort: Port[1]
    refers outputPort: Port[1]

    redefine tran {
        if (inputPort.hasOutgoing()) {
            inline outputPort.setIncomingItem(inputPort.outgoingItem)
        }
    }
}
